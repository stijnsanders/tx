[[@txDefs,DataLank,txSession,txFilter,txFilterSql,]][[!var
i,id,tid,oldTokTypeID,newTokTypeID,oldWeight,newWeight1,newWeight,RlmID:integer;
url:string;
desc:WideString;
filter,fs:string;
f:TtxFilter;
fq:TtxSqlQueryFragments;
qr:TQueryResult;
ids:TIdList;
]][[

//TODO: transaction?
CheckFormProtect(Context);
id:=Context['id'].AsInteger;
tid:=Context['tid'].AsInteger;
newTokTypeID:=Context['toktype'].AsInteger;
newWeight1:=Context['weight'].AsInteger;
desc:=Context['desc'].Value;
filter:=Context['filter'].Value;

ids:=TIdList.Create;
try
  if (filter='') then ids.Add(id) else
   begin
    tid:=0;
    f:=TtxFilter.Create;
    fq:=TtxSqlQueryFragments.Create(itObj);
    try
      fq.Fields:='Obj.id';
      f.FilterExpression:=filter;
      if f.ParseError<>'' then begin<<p style="color:red;">>=f.ParseError<</p>>end;
      fq.AddFilter(f);
      fq.Where:='Obj.rlm_id'+Session.Realms[rpView].SQL+' AND ('+fq.Where+')';
      fs:=fq.SQL;
    finally
      f.Free;
      fq.Free;
    end;

    qr:=TQueryResult.Create(Session.DbCon,fs,[]);
    try
      while qr.Read and Context.Connected do ids.Add(qr.GetInt('id'));
    finally
      qr.Free;
    end;

   end;

  for i:=0 to ids.Count-1 do
   begin
    id:=ids[i];
    if filter<>'' then tid:=0;

    //check realm
    if tid=0 then
     begin
      RlmID:=DBSingleValue('SELECT rlm_id FROM Obj WHERE id=?',[id],DefaultRlmID);
      oldTokTypeID:=0;
      oldWeight:=0;
     end
    else
     begin
      qr:=TQueryResult.Create(Session.DbCon,'SELECT Obj.rlm_id, Tok.toktype_id, Tok.weight FROM Tok INNER JOIN Obj ON Obj.id=Tok.obj_id WHERE Tok.id=?',[tid]);
      try
        if qr.Read then
         begin
          RlmID:=qr.GetInt('rlm_id');
          oldTokTypeID:=qr.GetInt('toktype_id');
          oldWeight:=qr.GetInt('weight');
         end
        else
         begin
          RlmID:=DefaultRlmID;//?
          oldTokTypeID:=0;
          oldWeight:=0;
         end;
      finally
        qr.Free;
      end;
     end;
    Session.HasRealmPermission(id,RlmID,rpEdit);

    //TODO: 'default': limit one token per branch!

    newWeight:=newWeight1;
    if (tid<>0) and (newTokTypeID<>oldTokTypeID) then
      newWeight:=newWeight-DBSingleValue('SELECT weight FROM TokType WHERE id=?',[oldTokTypeID],0);
    if (tid=0) or (newTokTypeID<>oldTokTypeID) then
      newWeight:=newWeight+DBSingleValue('SELECT weight FROM TokType WHERE id=?',[newTokTypeID],0);

    Session.DbCon.BeginTrans;
    try

        if tid=0 then
          tid:=Session.DbCon.Insert('Tok',
            ['obj_id',id
            ,'toktype_id',newTokTypeID
            ,sqlDesc,desc
            ,'weight',newWeight
            ,'c_ts',VNow
            ,'c_uid',Session.UserID
            ,'m_ts',VNow
            ,'m_uid',Session.UserID
            ],'id')
        else
          //move tokens not supported
          Session.DbCon.Update('Tok',
            ['id',tid
            ,'toktype_id',newTokTypeID
            ,sqlDesc,desc
            ,'weight',newWeight
            ,'m_ts',VNow
            ,'m_uid',Session.UserID
            ]);

        if newWeight<>oldWeight then Session.DbCon.Execute('UPDATE Obj SET weight=weight+(?) WHERE id=?',[newWeight-oldWeight,id]);

        //report
        Session.DbCon.Insert('Rpt',
          ['obj_id',id
          ,'ts',VNow
          ,'uid',Session.UserID
          ,sqlDesc,desc
          ,'weight',newWeight
          ,'toktype_id',newTokTypeId
          ]);

        TermStore.StoreTerms(itTok,tid,id,desc);
        if Use_ObjTokRefCache then Context.Include('aObjTokRefCache.xxmi',[id]);

        if Use_Unread and not Session.Stealth then
          Session.DbCon.Insert('Obx',['obj_id',id]);

        Session.DbCon.CommitTrans;
    except
        Session.DbCon.RollbackTrans;
        raise;
    end;

   end;
  if filter='' then
    case Context['x'].AsInteger of
      0:url:='Item.xxm?x=i'+IntToStr(id);
      1:url:='fTok.xxm?id='+IntToStr(id);
      2:url:='fRef.xxm?id='+IntToStr(id);
    end
  else
    url:='Modify.xxm?x=99&filter='+URLEncode(filter)+'&c='+IntToStr(ids.Count);
finally
  ids.Free;
end;
Session.AddFilterRecent(itTokType,newTokTypeID);
Session.UpdateID:=id;

Context.Redirect(url,true);
<<a href="[[=url]]">>=url<</a>
