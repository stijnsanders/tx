[[@txDefs,txSession,DataLank,Variants,]][[!var
qr:TQueryResult;
id:integer;
rootid:OleVariant;
url,v1,v2,e1,e2,l1,l2:string;
RefreshJournals:boolean;
]][[

if not(Session.IsAdmin('journals')) then
  raise Exception.Create('You''re not allowed to modify journals.');
CheckFormProtect(Context);

id:=Context['id'].AsInteger;
RefreshJournals:=false;

//assert Use_Journals

if id<>0 then
 begin
  qr:=TQueryResult.Create(Session.DbCon,
    'SELECT Jrl.view_expression,Jrl.edit_expression,Jrl.limit_expression'+
    ' FROM Jrl WHERE Jrl.id=?',[id]);
  try
    //Session.HasRealmPermission(qr.GetInt('id'),qr.GetInt('rlm_id'),rpEdit);
    v1:=qr.GetStr('view_expression');
    e1:=qr.GetStr('edit_expression');
    l1:=qr.GetStr('limit_expression');
  finally
    qr.Free;
  end;
 end;

{
Session.HasRealmPermission(objid,
  DBSingleValue('SELECT rlm_id FROM Obj WHERE id=?',[objid],DefaultRlmID),
  rpEdit);
}

rootid:=Context['root'].AsInteger;
if rootid=0 then rootid:=Null;

v2:=Context['view1'].Value;
if v2='' then v2:=Context['view2'].Value;
e2:=Context['edit1'].Value;
if e2='' then e2:=Context['edit2'].Value;
l2:=Context['limit1'].Value;
if l2='' then l2:=Context['limit2'].Value;
if (id=0) or (v1<>v2) or (e1<>e2) or (l1<>l2) then RefreshJournals:=true;

Session.DbCon.BeginTrans;
try
  if id=0 then
   begin
    id:=Session.DbCon.Insert('Jrl',
      ['name',Context['name'].Value
      ,'granularity',Context['granularity'].AsInteger
      ,'root_id',rootid
      ,'view_expression',v2
      ,'edit_expression',e2
      ,'limit_expression',l2
      ,'c_ts',VNow
      ,'c_uid',Session.UserID
      ,'m_ts',VNow
      ,'m_uid',Session.UserID
      ],'id');
    Session.DbCon.Insert('Jrt',
      ['jrl_id',id
      ,'name',Context['jrtname'].Value
      ,'icon',Context['jrticon'].AsInteger
      ]);
   end
  else
   begin
    Session.DbCon.Update('Jrl',
      ['id',id
      ,'name',Context['name'].Value
      ,'granularity',Context['granularity'].AsInteger
      ,'root_id',rootid
      ,'view_expression',v2
      ,'edit_expression',e2
      ,'limit_expression',l2
      ,'m_ts',VNow
      ,'m_uid',Session.UserID
      ]);
   end;

  if not(VarIsNull(rootid)) then
    Session.AddFilterRecent(itObj,rootid);

  Session.DbCon.CommitTrans;
except
  Session.DbCon.RollbackTrans;
  raise;
end;

if RefreshJournals then
 begin
  inc(JournalsCounter);
  Session.LoadJournalPermissions;
 end;

url:='Journals.xxm';//?id= ?
Context.Redirect(url,true);
<<a href="[[=url]]">>=url<</a>
