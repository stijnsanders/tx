[[@txDefs,DataLank,txSession,]][[!var
id,rid,pid,x,i,newObjTypeID,newWeight,RlmID:integer;
url:string;
desc:WideString;
]][[

//TODO: transaction?

if not(Session.IsAdmin('reports')) then
  raise Exception.Create('You''re not allowed to modify reports.');

rid:=Context['rid'].AsInteger;
pid:=Context['pid'].AsInteger;
newObjTypeID:=Context['objtype'].AsInteger;
newWeight:=Context['weight'].AsInteger+DBSingleValue('SELECT weight FROM ObjType WHERE id=?',[newObjTypeID],0);
CheckFormProtect(Context);

//check realm
RlmID:=DBSingleValue('SELECT rlm_id FROM Obj WHERE id=?',[pid],DefaultRlmID);
Session.HasRealmPermission(pid,RlmID,rpEdit);

desc:=Context['desc'].Value;

Session.DbCon.BeginTrans;
try

  id:=Session.DbCon.Insert('Obj',
    ['pid',pid
    ,'objtype_id',newObjTypeID
    ,'name',Context['name'].Value
    ,sqlDesc,desc
    ,'url',Context['url'].Value
    ,'weight',newWeight
    ,'rlm_id',RlmID
    ,'c_ts',VNow
    ,'c_uid',Session.UserID
    ,'m_ts',VNow
    ,'m_uid',Session.UserID
    ],'id');

  if Context['delrpt'].AsInteger=1 then
   begin
    //TODO: only if last report? own report?
    //TODO: replace with something?
    Session.DbCon.Execute('DELETE FROM Trl WHERE source=?',['rp'+IntToStr(rid)]);
    Session.DbCon.Execute('DELETE FROM Rpt WHERE id=?',[rid]);
   end;

  if Use_ObjPath then
   begin
    i:=0;
    Session.DbCon.Insert('ObjPath',['pid',id,'oid',id,'lvl',i]);
    while pid<>0 do
     begin
      inc(i);
      Session.DbCon.Insert('ObjPath',['pid',pid,'oid',id,'lvl',i]);
      pid:=DBSingleValue('SELECT pid FROM Obj WHERE id=?',[pid],0);
     end;
   end;

  if Use_Unread and not Session.Stealth then
    Session.DbCon.Insert('Obx',['obj_id',id]);

  TermStore.StoreTerms(itObj,id,id,desc);
  Session.AddFilterRecent(itObjType,newObjTypeID);

  Session.DbCon.CommitTrans;
except
  Session.DbCon.RollbackTrans;
  raise;
end;

x:=Context['relitem'].AsInteger;
if x=0 then
  url:='Item.xxm?x=i'+IntToStr(id)
else
  url:='fRef.xxm'+string(URLEncode(['id',id,'rid',0,'target',x]));//rid=0 since post values get re-posted on redirect!
Context.Redirect(url,true);
<<a href="[[=url]]">>=url<</a>
